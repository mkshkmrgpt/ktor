apply plugin: "kotlinx-serialization"

kotlin {
    targets {
        def current = []
        if (project.ext.ideaActive) {
            current = [posix]
        } else {
            if (project.ext.isMacosHost) current = [iosX64, iosArm64, macosX64]
            if (project.ext.isLinuxHost) current = [linuxX64]
            if (project.ext.isWinHost) current = [mingwX64]
        }

        configure(current) {
            compilations.main.cinterops {
                libcurl {
                    defFile 'posix/interop/libcurl.def'
                    includeDirs.headerFilterOnly '/opt/local/include', '/usr/local/include', '/usr/include', '/usr/include/x86_64-linux-gnu', '/usr/local/Cellar/curl/7.62.0/include'
                }
            }
        }
    }

    sourceSets {
        posixMain {
            dependencies {
                api project(':ktor-client:ktor-client-core')
                api project(':ktor-http:ktor-http-cio')
            }
        }
        posixTest.dependencies {
            api project(':ktor-client:ktor-client-features:ktor-client-logging')
            api project(':ktor-client:ktor-client-features:ktor-client-json')
            api "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"
        }
    }
}
