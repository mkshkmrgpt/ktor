apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

apply from: project.rootProject.file('gradle/pom.gradle')

task emptyJar(type: Jar) {
    classifier 'javadoc'
}

task stubJavadoc(type: Jar) {
    classifier = 'javadoc'
}

publishing {
    repositories {
        maven { url 'https://dl.bintray.com/kotlin/ktor' }
    }
    publications.all {
        pom.withXml(configureMavenCentralMetadata)

        switch (it.name) {
            case 'kotlinMultiplatform':
                task sourcesJar(type: Jar) {
                    classifier 'sources'
                    from kotlin.sourceSets.commonMain.kotlin
                }

                it.artifactId = "$project.name-native"
                it.artifact stubJavadoc
                it.artifact sourcesJar
                break
            case 'jvm':
                it.artifactId = "$project.name"
                break
            default:
                it.artifactId = "$project.name-$it.name"
                break
        }
    }

    kotlin.targets.all { target ->
        def publication = publishing.publications[targetName]
        if (publication != null) {
            publication.artifact stubJavadoc
        }
        if (target.platformType.name != 'native') {
            publication.gradleModuleMetadataFile = null
            tasks.matching { it.name == "generateMetadataFileFor${name.capitalize()}Publication" }.all {
                onlyIf { false }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publish = true
    override = true

    pkg {
        userOrg = 'kotlin'
        repo = 'ktor'
        name = 'ktor'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ktorio/ktor'

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

// TODO :kludge this is required for K/N publishing
bintrayUpload.dependsOn publishToMavenLocal

// This is for easier debugging of bintray uploading problems
bintrayUpload.doFirst {
    publications = project.publishing.publications.findAll { !it.name.contains('-test') }.collect {
        println("Uploading artifact '$it.groupId:$it.artifactId:$it.version' from publication '$it.name'")
        it
    }
}

